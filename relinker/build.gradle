apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.vanniktech.maven.publish"

group = project.properties["GROUP"]
version = project.properties["VERSION_NAME"]

android {
    compileSdkVersion 30
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    defaultConfig {
        minSdkVersion 9
    }
    testOptions {
        unitTests.all {
            testLogging {
                exceptionFormat 'full'
                showStackTraces true
                showCauses true
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }
}

dependencies {
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'ReLinker'
            description 'A robust native library loader for Android'
            url 'https://github.com/KeepSafe/ReLinker'
            inceptionYear 2015

            packaging 'aar'
            groupId project.group
            artifactId 'relinker'
            version project.version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/KeepSafe/ReLinker.git'
                url 'https://github.com/KeepSafe/ReLinker'
            }
            developers {
                developer {
                    name 'KeepSafe'
                }
            }
        }
    }
}

/*bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : ''
    key = project.hasProperty('bintray.apikey') ? project.property('bintray.apikey') : ''
    configurations = ['archives']
    pkg {
        repo = 'Android'
        name = 'ReLinker'
        userOrg = 'keepsafesoftware'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/KeepSafe/ReLinker.git'

        version {
            name = project.version
            desc = 'A robust native library loader for Android'
            released = new Date()
            vcsTag = project.version
        }
    }
}*/

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def getGpgKey() {
    return hasProperty("signingKey") ? signingKey : ""
}

def getGpgPassword() {
    return hasProperty("signingKeyPassword") ? signingKeyPassword : ""
}

mavenPublish {
    releaseSigningEnabled = !getGpgKey().isEmpty() && !getGpgPassword().isEmpty()
    targets {
        testMaven {
            releaseRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
            snapshotRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
        }
    }
}

signing {
    def signingKey = getGpgKey()
    def password = getGpgPassword()
    if (!signingKey.isEmpty() && !password.isEmpty()) {
        useInMemoryPgpKeys(signingKey, password)
    }
}


